group 'net.evendanan'

buildscript {
    repositories {
        google()
        maven { url 'https://jitpack.io' }
        maven { url 'https://plugins.gradle.org/m2/' }
        mavenCentral()
    }

    dependencies {
        classpath 'net.evendanan.autoversion:gradle-plugin:0.1.7'
//        classpath 'com.github.menny:GradleVersion:0.0.3'
        //classpath 'com.android.tools.build:gradle:3.3.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'net.evendanan.autoversion'

def generators = [
        new net.evendanan.versiongenerator.generators.EnvBuildVersionGenerator.CircleCi(
                1650/*adding to version. Should not ever change*/,
                -2575 - 2600 /*decrementing due to minor, every minor/major bump, this should be decremented*/),
        new net.evendanan.versiongenerator.generators.StaticVersionGenerator()
]

def versionData = versionGenerator.generateVersion(1, 10, 0, generators)

apply from: "${rootDir}/gradle/android_general.gradle"

android {
    defaultConfig {
        applicationId 'com.menny.android.anysoftkeyboard'
        minSdkVersion 19
        multiDexEnabled true
        versionCode versionData.versionCode
        versionName versionData.versionName

        println "Building AnySoftKeyboard ${versionData.versionName} (or ${project.version}), code ${versionData.versionCode}."

        //adding additional fields to the BuildConfig class.
        String support_email_address = System.getenv("ANYSOFTKEYBOARD_CRASH_REPORT_EMAIL")
        println 'crash report email is: ' + support_email_address

        buildConfigField "String", "CRASH_REPORT_EMAIL_ADDRESS", '"' + support_email_address + '"'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            if (file("/tmp/anysoftkeyboard.keystore").exists()) {
                storeFile file("/tmp/anysoftkeyboard.keystore")
                storePassword System.getenv("UPLOAD_ANYSOFTKEYBOARD_KEYSTORE_PASSWORD")
                keyAlias System.getenv("UPLOAD_ANYSOFTKEYBOARD_KEYSTORE_ALIAS")
                keyPassword System.getenv("UPLOAD_ANYSOFTKEYBOARD_KEYSTORE_KEY_PASSWORD")
                println "Using 'anysoftkeyboard.keystore' to release APK (with alias '${keyAlias}')."
            } else {
                println "Could not find 'anysoftkeyboard.keystore' file. Can not sign release APK with release keystore! Using debug."
                initWith signingConfigs.debug
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            debuggable false

            minifyEnabled true
            proguardFiles 'proguard-android-optimize.txt', 'proguard-rules.txt'

            buildConfigField("boolean", "TESTING_BUILD", "false")
        }

        debug {
            testCoverageEnabled true

            buildConfigField("boolean", "TESTING_BUILD", "true")
        }

        canary {
            initWith buildTypes.release

            buildConfigField("boolean", "TESTING_BUILD", "true")

            matchingFallbacks = ['release', 'debug']
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'src/main/java/com/menny/android/anysoftkeyboard/DAO_DB']
        }
    }
}

play {
    track = System.getProperty('deployChannel', 'alpha')
    serviceAccountEmail = System.getenv().getOrDefault('PUBLISH_APK_SERVICE_ACCOUNT_EMAIL', 'dummy@example.com')
    serviceAccountCredentials = file('/tmp/apk_upload_key.p12')
}

//verifying release-notes file
File playStoreWhatsNewFile = file("${project.projectDir}/src/main/play/release-notes/en-US/${System.getProperty('deployChannel', 'alpha')}.txt")
if (!playStoreWhatsNewFile.exists()) {
    throw new FileNotFoundException("Can not find whatsnew file for Play-Store upload!")
}

if (playStoreWhatsNewFile.text.length() > 500) {
    println("Locale " + Locale.getDefault())
    println("file encoding " + CharsetToolkit.defaultSystemCharset)
    println("File contents:")
    println("***" + playStoreWhatsNewFile.text + "***")
    throw new IllegalStateException("whatsnew file can not be longer than 500 characters! Currently " + playStoreWhatsNewFile.text.length())
}

dependencies {
    implementation project(':base')
    implementation project(':base-rx')
    implementation project(':nextword')
    implementation project(':dictionaries')
    implementation project(':jnidictionaryv1')
    implementation project(':jnidictionaryv2')
    implementation project(':prefs')
    implementation project(':overlay')
    implementation project(':pixel')

    implementation('org.sagebionetworks.bridge:android-sdk:0.2.0') {
        exclude group: 'com.google.code.findbugs'
    }

    //https://developer.android.com/jetpack/androidx/releases/work#declaring_dependencies
    def work_version = "1.0.1"
    implementation("android.arch.work:work-runtime:$work_version") {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }
    implementation("android.arch.work:work-rxjava2:$work_version") {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    implementation 'com.github.akarnokd:rxjava2-interop:0.13.2'

    implementation "com.anysoftkeyboard.api:api:$AnySoftKeyboardApiVersion"
    implementation "com.android.support:support-fragment:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:support-annotations:$supportLibVersion"
    implementation "com.android.support:palette-v7:$supportLibVersion"
    implementation "com.android.support:preference-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation 'com.jpardogo.materialtabstrip:library:1.1.0'
    implementation 'com.github.karczews:rx2-broadcast-receiver:1.0.5'
    implementation('com.github.menny.Chauffeur:permissions:0.1.1') {
        exclude group: 'com.android.support'
    }

    //Language-packs to be included in main AnySoftKeyboard app
    implementation 'com.anysoftkeybaord.languages:base:4.0.368@aar'
    implementation 'com.anysoftkeybaord.languages.english:english:4.0.368@aar'

    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    testImplementation project(path: ':base-test')
    testImplementation 'com.github.triplet.simpleprovider:simpleprovider:1.1.0'

//    // Room components
    /*
    * Room depends on 26.1 of support library, SupportLibrary does not promise interop between versions.
    * hence code breaking?
    * Trying to resolve by excluding from group
    *  */
    implementation("android.arch.persistence.room:runtime:$rootProject.roomVersion") {
        exclude group: 'com.android.support'
    }
    annotationProcessor "android.arch.persistence.room:compiler:$rootProject.roomVersion"
//     }
}

// trying to resolve the android version issues
//configurations.all {
//    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
//        def requested = details.requested
//        if (requested.group == 'com.android.support') {
//            if (!requested.name.startsWith("multidex")) {
//                details.useVersion '28.0.0'
//            }
//        }
//    }
//
//

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '25.4.0'
            }
        }
    }
}
